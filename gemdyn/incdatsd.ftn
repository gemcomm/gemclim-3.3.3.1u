!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!**s/r incdatsd - add dt days to olddat
!
      subroutine incdatsd(newdat, olddat, dt)

      implicit none

      character(len=16) newdat,olddat
      real(8) dt

!author 
!    Michel Desgagne - RPN - ?????
!
!revision
! v3_32 - Dugas B.          - use newdate/incdatr rather than pdfcdate and pdfjdate
!                             and account for the possible three-hourly resolution
!                             of these routines. The result is correct to the
!                             second from year 0 to 9999 inclusive.
!
!arguments I/O
! olddat   (I) - time in format "yyyymmdd.hhmmss"
! newdat   (O) - time in format "yyyymmdd.hhmmss"
! dt       (I) - time interval to add in units of days to olddat (can be negative)

      real(8) days,hours
      real hour_frac

      integer newyy,newmo,newdd,newhh,newmm,newss
      integer oldyy,oldmo,olddd,oldhh,oldmm,oldss,oldsign      
      integer tim1,tim2, oldstamp,newstamp, ier

      integer newdate
      external newdate, incdatr

      call prsdate( oldyy,oldmo,olddd,oldhh,oldmm,oldss,oldsign,olddat )

!     newhh, newmm and newss are calculated first
      hours = oldhh+(oldmm*60+oldss)/3600.0_8 ! old hours 
      hours = hours+dt*24.0_8 ! new hours

!     fractionnal hours in seconds
      hour_frac = nint( ( hours - int( hours,8 ) ) * 3600.0_8 )
      if (hour_frac == 3600.0) then
         hour_frac = 0.0
         hours = int( hours,8 )+1
      endif

      days  = int( hours/24.0_8,8 ) ! integer number of days to add

      if (hours < 0.0_8) then
         if (days*24.0_8 /= hours) days = days-1
         if (hour_frac /= 0.0) hour_frac = 3600.0+hour_frac
      endif

      newhh = hours-days*24.0_8 ! final number of hours
      newmm = hour_frac / 60.0_8 ! final number of minutes
      newss = nint( hour_frac - 60.0_8 * newmm ,8 ) 
      if (newss == 60) then
         newss = 0 ; newmm = newmm+1
      endif

      days  = days+olddd-1 ! days to add to oldyy/oldmm/01

!     calculate the date-time-stamp for oldyy/oldmm/01
      tim1 =  (oldyy * 100 + oldmo) * 100 + 1
      tim2 =   0

      ier = newdate( oldstamp, tim1,tim2, +3 )

      if (ier /= 0) then
         print *, 'incdatsd: error in call to newdate(+3), tim1,tim2= ',tim1,tim2
         stop ' in incdatsd'
      endif

      hours = int( days,8 )*24
!     add days*24 hours to oldstamp
      call incdatr( newstamp, oldstamp, hours )

      ier = newdate( newstamp, tim1,tim2, -3 )

      if (ier /= 0) then
         print *, 'incdatsd: error in call to newdate(-3), stamp= ',newstamp
         stop ' in incdatsd'
      endif

      newyy = mod( tim1/10000   , 10000 )
      newmo = mod( tim1/100     , 100 )
      newdd = mod( tim1         , 100 )

      write(newdat,16) newyy,newmo,newdd,newhh,newmm,newss
 16   format(i4.4,i2.2,i2.2,'.',i2.2,i2.2,i2.2)
      return
      end
      

