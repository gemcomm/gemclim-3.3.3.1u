***s/r nest_bcs_wndth - Set Winds at T=T0 in nesting zone
*
#include "model_macros_f.h"
*
      subroutine nest_bcs_wndth ()
*
      implicit none
*
*author M.Tanguay
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "vt0.cdk"
#include "nest.cdk"
*
      integer  vmmlod,vmmget,vmmuld
      external vmmlod,vmmget,vmmuld
*
      integer err,nvar,key(20),i,j,k
*
*----------------------------------------------------------------------
*
      if (north+south+west+east.lt.1) return 
*
      key (1) =VMM_KEY(nest_u)
      key (2) =VMM_KEY(nest_v)
      key (3) =VMM_KEY(nest_psd)
      nvar=3 
*
      key (nvar+1) =VMM_KEY(ut0)
      key (nvar+2) =VMM_KEY(vt0)
      key (nvar+3) =VMM_KEY(psdt0)
      nvar=nvar+3 
*
      err = vmmlod(key,nvar)
*
      err = VMM_GET_VAR(nest_u)
      err = VMM_GET_VAR(nest_v)
      err = VMM_GET_VAR(nest_psd)
*
      err = VMM_GET_VAR(ut0)
      err = VMM_GET_VAR(vt0)
      err = VMM_GET_VAR(psdt0)
*
      if (l_north) then
         do k=1,G_nk
            do j=l_nj-pil_n+1,l_nj
            do i=1,l_ni
               psdt0(i,j,k) = nest_psd(i,j,k)            
            end do
            end do
            do j=l_nj-pil_n+1,l_nj
            do i=1,l_niu
               ut0 (i,j,k) = nest_u (i,j,k)
            end do
            end do
            do j=l_nj-pil_n,l_njv
            do i=1,l_ni
               vt0 (i,j,k) = nest_v (i,j,k)
            end do
            end do
         end do
      endif
*
      if (l_east) then
         do k=1,G_nk
            do j=1,l_nj
            do i=l_ni-pil_e+1,l_ni
               psdt0(i,j,k) = nest_psd(i,j,k)            
            end do
            end do
            do j=1,l_nj
            do i=l_ni-pil_e,l_niu
               ut0 (i,j,k) = nest_u (i,j,k)
            end do
            end do
            do j=1,l_njv
            do i=l_ni-pil_e+1,l_ni
               vt0 (i,j,k) = nest_v (i,j,k)
            end do
            end do
         end do
      endif
*
      if (l_south) then
         do k=1,G_nk
            do j=1,pil_s
            do i=1,l_ni
               vt0  (i,j,k) = nest_v  (i,j,k)
               psdt0(i,j,k) = nest_psd(i,j,k)            
            end do
            end do
            do j=1,pil_s
            do i=1,l_niu
               ut0 (i,j,k) = nest_u (i,j,k)
            end do
            end do
         end do
      endif
*
      if (l_west) then
         do k=1,G_nk
            do j=1,l_nj
            do i=1,pil_w
               ut0  (i,j,k) = nest_u  (i,j,k)
               psdt0(i,j,k) = nest_psd(i,j,k)            
            end do
            end do
            do j=1,l_njv
            do i=1,pil_w
               vt0 (i,j,k) = nest_v (i,j,k)
            end do
            end do
         end do
      endif
*
      err = vmmuld(key,nvar)
*
*----------------------------------------------------------------------
      return
*
      end
